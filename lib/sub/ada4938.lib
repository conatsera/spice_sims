* ADA4938 Spice-model 
* Description: Amplifier
* Generic Desc: Ultra-Low Distortion Diff ADC Driver
* Developed by: TRW, CK, ADI
* Revision History: 08/10/2012 - Updated to new header style
* 1.0 (05/2008)
* Copyright 2006, 2012 by Analog Devices, Inc.
*
* Refer to http://www.analog.com/Analog_Root/static/techSupport/designTools/spiceModels/license/spice_general.html for License Statement. Use of this model
* indicates your acceptance with the terms and provisions in the License Statement.
*
* BEGIN Notes:
*
* Not Modeled:
*     distortion is not characterized
*     cmrr is not characterized in this version.
*
* Parameters modeled include:
*     closed loop gain and phase vs bandwidth
*     output voltage limiting
*     offset voltage (is  non-static, will  vary with gain)
*     ibias (again, is static, will not vary with vcm)
*     slew rate and step response performance (estimated)
*     (slew rate is based on 10-90% of step response)
*     current on output will be reflected to the supplies 
*     Vocm is varable and include input typical offset
*     V(Inoise) is characterized
*     I(Inoise) is characterized 
*
* END Notes
*
* Node assignments
*               FD output negative
*                | FD output positive
* 		 | |  non-inverting input
*		 | |  | inverting input
*		 | |  |  |  positive supply
*		 | |  |  |  |  negative supply
*		 | |  |  |  |  |  output positive
*		 | |  |  |  |  |  |   output negative
*		 | |  |  |  |  |  |   |  vocm input
*		 | |  |  |  |  |  |   |  |  
.SUBCKT ADA4938 9b 3b 3a 9 99 50 71b 71 110 

*** positive input left side ***

I 99 5 4E-4
Q1 50 2 5 QX
vos 3a 2 -1.95E-3

*** RAIL CLIPING ***

Dlim+ 75a 14b Dx
Vlim+ 99 14b 1.92
Dlim 14c 75a Dx
Vlim 14c  50 1.92
Dlim- 13b 76a DX
Vlim- 13b 50 1.92
Dlim-B 76a 13C Dx
Vlim-B 99 13C 1.92

*** VOCM INPUT RAIL CLIPING ***

DOCMa 100 100A DX
VOCMa 99 100A 1.95
DOCMb 100b 100 DX
VOCMb 100b 50 1.95

*** negative input right side ***

I1 99 6 4E-4
Q2 50 9a 6 QX

*** Input capacitance/impedance ***

Cin 3a 9 1p

*** pole, zero pole stage ***

G1 13 14 5 6 5E-3
c1 14 13 0.34p
c2 98 13 0.005f
c3 98 14 0.005f
r11 98 13 RX1 250K 
r12 98 14 RX1 250K 

*** pole zero stage( POSITIVE SIDE) ***

gp1 0 75 14 98 0.7
RP1 75 0 RX2 1
CP1 0 75 0.55p

*** pole zero stage( NEGATIVE SIDE) ***

gp2 0 76 13 98 0.7
RP2 76 0 RX2 1
CP2 0 76 0.55p

*** output stage Negative side ***

D17 76 84 DX
VO1  84 70 .177V
VO2  70 85 .177V
D16 85 76  DX
G30 70 99c 99 76  91E-3
G31 98c 70 76 50  91E-3
RO30 70 99c RX2 11
RO31 98c 70 RX2 11 
VIOUT1 99 99c 0
VIOUT2 98c 50 0
VIOUT3 70 71 0

*** Output Stage Positive side ***

D17b 75 84b DX
VO1b  84b 70b .177V
VO2b  70b 85b .177V
D16b 85b 75  DX
G30b 70b 99d 99 75  91E-3
G31b 98d 70b 75 50  91E-3
RO30b 70b 99d RX2 11
RO31b 98d 70b RX2 11 
VIOUTB1 99 99d 0
VIOUTB2 98d 50 0
VIOUTB3 70b 71b 0

*** VOCM STAGE ***

Gocm_a 0 75 110 0 1
Gocm_b 0 76 110 0 1
Rocm1 100 99 400K 
Rocm2 50 100 400K 

Voffset 100 110 -1E-3

*** Noise ***
VEN 9 9a 0
*EN 9 9a 36n 0 1
*GN1 0 3a 94n 0 0.01
*GN2 0 9 97n 0 0.01

*DN1 35n 36n DEN
*DN2 36n 37n DEN
*VN1 35n 0 2.5
*VN2 0 37n 2.5

*****************Negative Current Noise********************

F_FNnoise         0 2 V_VNnoise 1
V_VNnoise         0 500 0  
D_DNnoise         0 500 DEN  
R_RNnoise         0 500 RX3 55  

*****************Positive Current Noise********************

F_FPnoise         0 9a V_VPnoise 1
V_VPnoise         0 501 0  
D_DPnoise         0 501 DEN  
R_RPnoise         0 501 RX3 55  

*** CURRENT MIRROR TO SUPPLIES POSITVIE SIDE ***

FO1 0 99 poly(2) VIOUT1 VI1 -19.803E-3 1 -1
FO2 0 50 poly(2) VIOUT2 VI2 -19.803E-3 1 -1
FO3 0 400 VIOUT1 1
VI1 401 0 0
VI2 0 402 0
DM1 400 401 DX
DM2 402 400 DX 

*** CURRENT MIRROR TO SUPPLIES NEGATIVE SIDE ***

FO1B 0 99 poly(2) VIOUTB1 VIB1 -19.803E-3 1 -1
FO2B 0 50 poly(2) VIOUTB2 VIB2 -19.803E-3 1 -1
FO3B 0 400B VIOUTB1 1
VIB1 401B 0 0
VIB2 0 402B 0
DMB1 400B 401B DX
DMB2 402B 400B DX

*** Power Down feature ***
Vpd 66 99 0
IQc 67 50 1E-3
Qpd1 69 68 67 QY
Qpd2 65 66 67 QY
Vpd1 99 69 0
Vpd2 99 65 0
Fpd1 50 99 Vpd1 0.4
Fpd2 99 50 Vpd2 18
Vpdr 99 68 2.2


*** Reference Stage ***

Eref 98 0 poly(2) 99 0 50 0 0 0.5 0.5

*** Feadback output ***

Vfd+ 3b 71b 0
Vfd- 9b 71  0

*** Buffer Stage ***

Vbf1 76 76a 0
Vbf2 75 75a 0

*** Overdrive Protection ***

Vsupp 99d 50d 10.6
Dovd1 99 99d DOVD
Dovd2 50d 50 DOVD

*** Models ***

.MODEL QX PNP (BF=228 Is=1E-15 KF=5E-13 AF=1)
.MODEL QY NPN (BF=228 Is=1E-15 KF=5E-13 AF=1)
.MODEL RX1 Res(T_abs=0)
.MODEL RX2 Res(T_abs=0)
.MODEL RX3 Res(T_abs=0)
.MODEL DX D(IS=1E-14) 
.MODEL DEN D(IS=1E-9 RS=1 KF=1E-16 AF=1)
.MODEL DOVD D(IS=1E-15)
.ENDS

*$





